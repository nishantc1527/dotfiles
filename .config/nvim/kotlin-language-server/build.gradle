plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
}

repositories {
    mavenCentral()
}

configure(subprojects.findAll { it.name != 'grammars' }) {
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    publishing {
        repositories {
            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/fwcd/kotlin-language-server')
                credentials {
                    username = project.findProperty('gpr.user') ?: System.getenv('GPR_USERNAME')
                    password = project.findProperty('gpr.key') ?: System.getenv('GPR_PASSWORD')
                }
            }
        }

        publications {
            gpr(MavenPublication) {
                from components.java
            }
        }
    }
}
